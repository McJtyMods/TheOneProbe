plugins {
    id 'eclipse'
    id 'idea'
    id 'maven-publish'
    id "com.modrinth.minotaur" version "2.+"
    id "com.matthewprenger.cursegradle" version "1.4.0"
    id 'net.neoforged.gradle.userdev' version '7.0.163'
}

if (project.file('../gradletools.gradle').exists()) {
    apply from: '../gradletools.gradle'
} else {
    apply from: 'https://raw.githubusercontent.com/McJtyMods/MultiWorkspace/1.21_neo/gradletools.gradle'
}

group= "mcjty.theoneprobe" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
base.archivesName = "theoneprobe"
java.toolchain.languageVersion = JavaLanguageVersion.of(21)

if (System.getenv('BUILD_NUMBER') != null) {
    version += "-${System.getenv('BUILD_NUMBER')}"
}

repos()
runs()
jars()

sourceSets.main.resources {
    srcDir 'src/generated/resources'
}

dependencies {
    mc()
    jei()
}

cfdeps([], [], [])
modrinth([], [], [])

subsystems {
    parchment {
        minecraftVersion = "${parchment_minecraft_version}"
        mappingsVersion = "${parchment_version}"
    }
}

task apiJar(type: Jar) {
    archiveClassifier = "api"
    from(sourceSets.main.output) {
        include "mcjty/theoneprobe/api/**"
    }
}

artifacts {
    archives apiJar
}

publishing {
    publications.create('mavenJava', MavenPublication) {
        from components.java
        artifact apiJar

        artifactId = project.archivesBaseName
        pom {
            name = 'RFToolsBase'
            description = 'Base mod for all McJty tech mods'
            scm {
                url = 'https://github.com/McJtyMods/RFToolsBase'
                connection = 'scm:git:git@github.com:McJtyMods/RFToolsBase.git'
                developerConnection = 'scm:git:git@github.com:McJtyMods/RFToolsBase.git'
            }
            issueManagement {
                system = 'github'
                url = 'https://github.com/McJtyMods/RFToolsBase/issues'
            }
            licenses {
                license {
                    name = 'MIT'
                    distribution = 'repo'
                }
            }
            developers {
                developer {
                    id = 'McJty'
                    name = 'McJty'
                    roles = ['developer']
                }
            }
        }
    }
    repositories {
        if (System.getenv('local_maven') != null) {
            maven {
                name = 'local'
                url = 'file://' + System.getenv('local_maven')
            }
        }
    }
}
